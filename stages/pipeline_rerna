#!/usr/bin/env perl 

use strict;
use warnings;
no warnings('once');

use Pipeline;
use Pipeline_Conf;

use Getopt::Long;
use File::Copy;
umask 000;

my $stage_name = "rerna";
my $revision = "0";
my $stage_id = '450';
my $version  = $Pipeline_Conf::pipeline_version.".".$revision;
my $default_nr = $Pipeline_Conf::md5rna_full;

# options
my $job_num = "";
my $fasta   = "";
my $rna_nr  = "";
my $ver     = 0;
my $help    = 0;
my $options = GetOptions ("job=i"     => \$job_num,
			  "fasta=s"   => \$fasta,
			  "rna_nr=s"  => \$rna_nr,
			  "version!"  => \$ver,
			  "help!"     => \$help,
			 );
if ( $ver ) {
  print STDERR "$stage_name - $version - $stage_id\n";
  exit(0);
} elsif ( $help or !($job_num and $fasta and $rna_nr) ) {
  print STDERR "Usage: pipeline_$stage_name -j <job number> -f <fasta file> -r <rna NR>\n";
  exit(1);
}

my $log = Pipeline::logger($job_num);
$log->info("Starting $stage_name on job: $job_num");

unless ($rna_nr && (-e $rna_nr)) {
  $rna_nr = $default_nr;
  $log->info("changing rna_nr to $default_nr");
}
unless ((-e $fasta) && (-e $rna_nr)) {
  $log->error("file: $fasta or $rna_nr does not exist");
  exit(1);
}

# update jobcache stage status
Pipeline::update_stage_info($job_num, $stage_name, "running");

my $job_suffix = substr $job_num, -2;
$job_suffix = "0".$job_suffix if length($job_suffix) == 1;
my $job_dir     = $Pipeline_Conf::global_job_dir."/".$job_num;
my $proc_dir    = $job_dir."/proc";
my $stage_dir   = $proc_dir."/".$stage_id.".".$stage_name;
my $results_dir = $Pipeline_Conf::recompute_dir."/jobs/".$job_suffix;
my $run_dir     = $stage_dir;
my $hostname    = `hostname`;
chomp $hostname;

# create directories
if (-d $stage_dir) {
  my $t = time;
  $log->info("found $stage_dir, moving to $stage_dir.$t");
  move($stage_dir, $stage_dir.".".$t) or fail($log, "$!");
}
mkdir($stage_dir) or fail($log, "Could not mkdir: $stage_dir, $!");

# set cluster workspace dir (use local if large job)
if (-d $Pipeline_Conf::cluster_workspace) {
  $run_dir = $Pipeline_Conf::cluster_workspace."/$job_num.$stage_id.$stage_name";
  if (-d $run_dir) {
    system("rm -rf $run_dir");
  }
  mkdir($run_dir) or fail($log, "Could not mkdir: $run_dir, $!");
  system("echo $hostname > $stage_dir/hostname");
}
$log->info("Running on host $hostname, using dir $run_dir");

my $prefix   = $stage_id.".".$stage_name;
my $err_file = $stage_dir."/".$prefix.".err";
my $message  = "$stage_name failed on job: $job_num, see $err_file for details.";

# run blat / bleach
my $fasta_file = "$run_dir/$prefix.fna";
my $sims_file  = "$run_dir/$prefix.sims";
system("cp $fasta $fasta_file") == 0 or fail($log, "cp $fasta: ".$message);
system("blat -out=blast8 -t=dna -q=dna -fastMap $rna_nr $fasta_file stdout | bleachsims -s - -o $sims_file -r 0 > $run_dir/blat.out 2> $err_file") == 0 or fail($log, "blat: ".$message);

# new sims calculated, add m5rna version to JobAttributes
Pipeline::set_job_attributes($job_num, [['m5rna_sims_version', $Pipeline_Conf::ach_sequence_ver]]);

# cleanup
my $final_file = "$results_dir/$job_num.recompute.rna.sims.gz";
system("gzip $sims_file") == 0 or fail($log, "gzip: ".$message);
system("cp $sims_file.gz $final_file") == 0 or fail($log, "cp $sims_file.gz: ".$message);
system("rm -rf $run_dir");

$log->info("Finished $stage_name on job: $job_num");

# update jobcache stage status
Pipeline::update_stage_info($job_num, $stage_name, "completed");

exit(0);

sub fail {
  my ($log, $message) = @_;
  Pipeline::update_stage_info($job_num, $stage_name, "error");
  $log->error($message);
  if ($run_dir ne $stage_dir) {
    system("mv $run_dir/* $stage_dir/.");
    system("rmdir $run_dir");
  }
  exit(1);
}
